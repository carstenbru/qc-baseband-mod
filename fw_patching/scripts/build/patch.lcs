/**
 * @file patch.lcs
 * @brief patch linker control script
 *
 * This file handles the correct placement of the patch code in memory.
 *
 * @author Carsten Bruns (carst.bruns@gmx.de)
 */

OUTPUT_FORMAT ("elf32-littleqdsp6",
               "elf32-bigqdsp6",
           "elf32-littleqdsp6")
OUTPUT_ARCH (hexagon)

PHDRS
{
    text PT_LOAD;
    restricted.data PT_LOAD;
    data PT_LOAD;
    rodata PT_LOAD;
    bss PT_LOAD;
}

SECTIONS
{
    . = __patch_addr_text_base__;
    .text : ALIGN(0x1000) { 
        *(.text*) 
    } : text
    
    . = DEFINED(__patch_addr_data_base__) ? __patch_addr_data_base__ : .;
    .data : ALIGN(0x1000) {
        *(.data*)
        *(.scommon*)
    } : data
    .rodata : ALIGN(0x1000) {
        *(.rodata*)
    } : rodata
    .bss : ALIGN(0x1000) {
        *(.bss*)
    } : bss
    
    . = DEFINED(__patch_addr_data_restricted_base__) ? __patch_addr_data_restricted_base__ : .;
    .restricted.data : ALIGN(0x1000) {
        *(.restricted.data*)
    } : restricted.data
}
